var @commits = cmd { git log --since="yesterday" }
var @prs = cmd { gh pr list --json title,url,createdAt }

exe @claude(request) = cmd { claude -p "@request" }
exe @formatPRs(items) = js {
  return items.map(pr => `- PR: ${pr.title} (${pr.url})`).join('\n');
}

var @standup = `
  Write a standup update in markdown summarizing the work I did
  yesterday based on the following commits and PRs.

  ## Commits:
  @commits

  ## PRs:
  @formatPRs(@prs)
`

exe @reviewPrompt(input) = `
Review the following standup update to ensure I'm not taking
credit for work I didn't do.

My username is @githubuser. Here's my standup update:
<standup>
@input
</standup>

Check whether there are any commits or PRs listed that I wasn't
involved in. Respond with APPROVE or DENY in all caps.
`

exe @hasApproval(text) = @text.toLowerCase().includes("approve")

exe @review(input) = when [
  let @check = @claude(@reviewPrompt(@input))
  @hasApproval(@check) => @input
  @mx.try < 3 => retry
  * => "No definitive answer"
]

show @claude(@standup) | show "Reviewing #@mx.try..." | @review
