import { @claude, @haiku } from "@lib/claude.mld"

>> Classify the most important task from a plan file
exe @classifyTask(plan) = [
  let @prompt = `Identify the SINGLE most important next task:
@plan
Return JSON: { "task": "...", "type": "implement|fix|test" }`
  => @haiku(@prompt) | @json.llm
]

>> Build context (load only relevant specs)
exe @buildContext(task, specs) = [
  let @relevant = for @spec in @specs
    when @task.type in @spec.mx.relative => @spec
  => { specs: @relevant }
]

>> Execute the task with full agent
exe @executeTask(task, context) = @claude(`
# Task: @task.task
# Specs: @context.specs.join("\n")
Implement this. Search before assuming not implemented.
`, "sonnet", ".", "Read,Edit,Write,Bash,Grep,Glob")

>> Validate with tests
exe @validate() = [
  let @out = cmd { npm test 2>&1 }
  => { pass: @out.exitCode == 0 }
]

>> The loop
loop(endless) until @state.stop [
  var @plan = <fix_plan.md>
  when @plan.trim() == "" => done "complete"

  let @task = @classifyTask(@plan)
  let @context = @buildContext(@task, <specs/*.md>)
  let @result = @executeTask(@task, @context)
  let @check = @validate()

  when @check.pass => run cmd { git commit -am "@task.task" && git push }
  continue
]