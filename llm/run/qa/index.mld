>> QA Testing Orchestrator v2 (Five-Phase)
>> Usage: mlld run qa [--tier 1] [--topic <filter>] [--topics <list>]
>>
>> Resilience: exe llm labels + checkpoint directives. Crash recovery is automatic.
>>   mlld run qa --tier 1                       # auto-resumes via cache
>>   mlld run qa --tier 1 --resume "review"     # skip to self-review phase
>>   mlld run qa --tier 1 --new                 # fresh run
>>
>> Phases:
>>   Phase 0 (Triage): Decision agent picks topics intelligently
>>   Phase 1 (Flail): Agents test with limited access (mlld howto only)
>>   Phase 2 (Self-Review): Agents resume and check their work against tests/cookbook
>>   Phase 3 (Trends): Single agent analyzes all results for cross-cutting patterns
>>   Phase 4 (Strategy): Generates strategy-questions.md from trend report for human review
>>
>> Examples:
>>   mlld run qa --tier 1                           # All phases
>>   mlld run qa --tier 1 --resume "trends"         # Skip to trend analysis
>>   mlld run qa --topic variables                  # Test variables-* topics
>>   mlld run qa --topics when-inline,for-arrow     # Test specific list

/needs { sh }

import { @loadTopics, @filterTopics } from "./lib/topics.mld"
import { @loadAllResults, @loadAllReviews, @buildRunSummary, @buildDocClarityReport } from "./lib/aggregate.mld"
import { @runTriage } from "./phases/triage.mld"
import { @runFlail } from "./phases/flail.mld"
import { @runReview } from "./phases/review.mld"
import { @runTrends } from "./phases/trends.mld"
import { @runStrategy } from "./phases/strategy.mld"

>> Parse CLI options
import "@payload" as @p
var @topic = @p.topic ? @p.topic : ""
var @topics = @p.topics ? @p.topics : ""
var @tier = @p.tier ? @p.tier : ""

>> Shell helpers
exe @mkdirp(dir) = sh { mkdir -p "$dir" }

>> Run directory â€” fixed per run (checkpoint handles resumption)
var @qaRoot = "@base/qa/runs"
var @today = @now.slice(0, 10)
var @runDir = `@qaRoot/@today`
run @mkdirp(`@runDir/topics`)

>> Store git commit for future triage diffing
exe @currentCommit() = cmd {git rev-parse HEAD}

>> Load and filter topics
var @allTopics = @loadTopics(@tier)
var @hasExplicitTopics = @topic || @topics

show `=== QA Run: @runDir ===`
show ``

when @allTopics.length == 0 => show `No topics match tier filter: @tier`

>> ========== PHASE 0: TRIAGE ==========

checkpoint "triage"

exe @latestRunId() = sh {ls -1 "@qaRoot" 2>/dev/null | grep '^20' | sort -r | head -1}

exe @doTriage(runDir, allTopics) = [
  when @hasExplicitTopics => @allTopics
  show `=== PHASE 0: TRIAGE ===`
  let @lastRunId = @latestRunId()
  let @lastRunIdTrimmed = @lastRunId.trim()
  let @lastRunDir = @lastRunIdTrimmed && @lastRunIdTrimmed != @today ? `@qaRoot/@lastRunIdTrimmed` : null
  let @selected = @runTriage(@runDir, @allTopics, @lastRunDir)
  show ``
  => @selected
]

var @triageTopics = @doTriage(@runDir, @allTopics)

>> Apply explicit filters (bypass triage)
var @activeTopics = @filterTopics(@triageTopics, @topic, @topics)

show `Topics: @activeTopics.length`
show ``

when @activeTopics.length == 0 => show `No topics match filter: @topic @topics\nAvailable: @allTopics.join(", ")`

>> ========== PHASE 1: FLAIL ==========

checkpoint "flail"

show `=== PHASE 1: FLAIL ===`
run @runFlail(@runDir, @activeTopics)
show ``

>> ========== PHASE 2: SELF-REVIEW ==========

checkpoint "review"

show `=== PHASE 2: SELF-REVIEW ===`
run @runReview(@runDir, @activeTopics)
show ``

>> ========== SUMMARY ==========
show `=== QA SUMMARY ===`

>> Aggregate results for display (per-file loading repairs malformed agent JSON)
var @allResultsList = @loadAllResults(@runDir)
var @allReviewsList = @loadAllReviews(@runDir)
var @allSessions = <@runDir/topics/**/session.json>?
var @allSessionsList = @allSessions ? @allSessions : []
var @passing = for @r in @allResultsList when @r.status == "pass" => @r
var @failing = for @r in @allResultsList when @r.status == "fail" => @r
var @partial = for @r in @allResultsList when @r.status == "partial" => @r
var @blockedPhase1 = for @s in @allSessionsList when @s.phase1_blocked => @s
var @blockedPhase2 = for @s in @allSessionsList when @s.phase2_blocked => @s

show `Experiments: @allResultsList.length total`
show `  Passing: @passing.length`
show `  Failing: @failing.length`
show `  Partial: @partial.length`
show `Self-reviews: @allReviewsList.length`
when @blockedPhase1.length > 0 => [
  show `  Blocked (phase 1): @blockedPhase1.length`
  for @b in @blockedPhase1 => show `    - @b.topic: @b.phase1_blocked_reason`
]
when @blockedPhase2.length > 0 => [
  show `  Blocked (phase 2): @blockedPhase2.length`
  for @b in @blockedPhase2 => show `    - @b.topic: @b.phase2_blocked_reason`
]
show ``

>> Doc clarity report
var @docClarity = @buildDocClarityReport(@runDir)
when @docClarity.summary.docs_could_be_clearer > 0 || @docClarity.summary.docs_genuinely_misleading > 0 => [
  show `=== DOC CLARITY INSIGHTS ===`
  show `QA needed more exploration: @docClarity.summary.qa_insufficient_exploration`
  show `Docs could be clearer: @docClarity.summary.docs_could_be_clearer`
  show `Docs genuinely misleading: @docClarity.summary.docs_genuinely_misleading`
  output @docClarity to "@runDir/doc-clarity-report.json"
  show `Report: @runDir/doc-clarity-report.json`
  show ``
]

>> ========== PHASE 3: TRENDS ==========

checkpoint "trends"

show `=== PHASE 3: TRENDS ===`
run @runTrends(@runDir, @activeTopics)
show ``

>> ========== PHASE 4: STRATEGY ==========

checkpoint "strategy"

show `=== PHASE 4: STRATEGY ===`
run @runStrategy(@runDir)
show ``

>> ========== RUN SUMMARY ==========
var @topicSummaries = @buildRunSummary(@runDir, @activeTopics)

var @runSummary = {
  run: @today,
  date: @now,
  tier: @tier,
  topic_count: @activeTopics.length,
  experiment_count: @allResultsList.length,
  passing: @passing.length,
  failing: @failing.length,
  partial: @partial.length,
  topics: @topicSummaries
}

output @runSummary to "@runDir/run-summary.json"
show `Run summary: @runDir/run-summary.json`

>> Cross-run progress tracking
var @allRunSummaries = <@qaRoot/*/run-summary.json>?
var @allRunSummariesList = @allRunSummaries ? @allRunSummaries : []
var @progress = {
  generated_at: @now,
  total_runs: @allRunSummariesList.length,
  runs: @allRunSummariesList
}
output @progress to "@qaRoot/progress.json"
show `Progress tracker: @qaRoot/progress.json (@allRunSummariesList.length runs)`

