>> QA Testing Orchestrator
>> Usage: mlld run qa [--tier 1] [--topic <filter>] [--topics <list>]
>> Runs parallel QA testing across mlld howto topics
>> Examples:
>>   mlld run qa --tier 1                           # Test tier 1 only (core syntax)
>>   mlld run qa --tier 1,2                         # Test tier 1 and 2
>>   mlld run qa --topic variables                  # Test variables-* topics
>>   mlld run qa --topics when-simple,for-arrow     # Test specific list

import { @claude } from @mlld/claude

>> Get filters from payload (namespace import for optional fields)
import "@payload" as @p
var @topic = @p.topic ? @p.topic : ""
var @topics = @p.topics ? @p.topics : ""
var @tier = @p.tier ? @p.tier : ""

>> Load all atoms dynamically from docs/src/atoms
var @atomFiles = <@base/docs/src/atoms/**/*.md>

>> Extract topics with qa_tier from atom frontmatter
var @tiers = @tier ? @tier.split(",") : []
var @allTopics = for @f in @atomFiles when @f.fm.qa_tier && @f.fm.id && !@f.fm.id.startsWith("_") && (@tiers.length == 0 || @tiers.includes(`@f.fm.qa_tier`)) => @f.fm.id

>> Configuration
var @outputDir = "@base/qa"
var @parallelLimit = 20

>> Filter topics based on --topic (prefix) or --topics (comma list)
exe @filterTopics(all, filter, list) = when first [
  >> --topics (comma list) takes precedence
  @list => for @t in @all when @list.split(",").includes(@t) => @t
  >> --topic: prefix/exact match filter
  @filter => for @t in @all when @t == @filter || @t.startsWith(@filter) => @t
  >> No filter: return all
  * => @all
]

var @activeTopics = @filterTopics(@allTopics, @topic, @topics)

>> Load prompt template (not using `template` directive - it executes mlld code blocks)
var @promptTemplate = <@base/llm/run/qa-prompt.att>

>> Build prompt by replacing placeholders
exe @buildPrompt(tmpl, topic, outputDir) = [
  let @s1 = @tmpl.replace("@topic", @topic)
  => @s1.replace("@outputDir", @outputDir)
]

>> Create output directory
run cmd {mkdir -p @outputDir}

>> Check if we have topics to test
when @activeTopics.length == 0 => show `No topics match filter: @topic\nAvailable: @allTopics.join(", ")`

>> Process each topic
show `Starting QA testing for @activeTopics.length topics...`
show `Output directory: @outputDir`
show `Parallel limit: @parallelLimit`
show ``

>> hardcoded parallel until parallel() can take vars
var @results = for parallel(20) @t in @activeTopics [
  let @prompt = @buildPrompt(@promptTemplate, @t, @outputDir)
  let @topicDir = `@outputDir/@t`
  run cmd {mkdir -p @topicDir}
  show `Starting: @t`
  let @response = @claude(@prompt, "sonnet", @base, "Read,Write,Bash,Glob,Grep") | log
  => @t
]

>> Summary
show ``
show `QA testing complete.`
show `Tested @results.length topics`
show ``
show `Results in: @outputDir`
show ``
show `To aggregate results:`
show `  find qa/ -name "results.json" -exec cat {} \\; | jq -s '.'`
